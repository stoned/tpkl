import "tpkl:tpkl"

shArgsFromEnv: tpkl.Command =
  """
  set -e
  echo "argc=${TPKL_TASK_ARGC}:"
  echo "arg0=${TPKL_TASK_ARG_0}:"
  echo "arg1=${TPKL_TASK_ARG_1}:"
  echo "arg2=${TPKL_TASK_ARG_2}:"
  # echo last task argument
  if [ "${TPKL_TASK_ARGC}" -gt 0 ]; then
    eval echo "argL=\\${TPKL_TASK_ARG_$((TPKL_TASK_ARGC - 1))}:"
  fi
  """ |> tpkl.sh

shArgs: tpkl.Command =
  """
  set -- "$@"
  echo "argc=$#:"
  echo "arg0=$0:"
  echo "arg1=$1:"
  echo "arg2=$2:"
  echo "arg3=$3:"
  """ |> tpkl.sh

tasks: tpkl.Tasks = new {
  // Task arguments from environment variables without environment
  // inheritance
  ["sh-env-args-no-inherit"] {
    inheritEnv = false
    cmds {
      shArgsFromEnv
    }
  }

  // Task arguments from environment variables with environment
  // inheritance
  ["sh-env-args-inherit"] {
    inheritEnv = true
    cmds {
      shArgsFromEnv
    }
  }

  // Call task getting arguments from environment variables without
  // environment inheritance
  ["call-no-inherit"] {
    cmds {
      "sh-env-args-no-inherit" |> tpkl.task
    }
  }

  // Call task getting arguments from environment variables with
  // environment inheritance
  ["call-inherit"] {
    cmds {
      "sh-env-args-inherit" |> tpkl.task
    }
  }

  // Task arguments from property and helper method
  ["args-properties"] {
    cmds {
      """
      echo 'argc=\(tpkl.argc):'
      echo 'arg0=\(tpkl.arg(0)):'
      echo 'arg1=\(tpkl.arg(1)):'
      echo 'arg2=\(tpkl.arg(2)):'
      echo 'arg3=\(tpkl.arg(3)):'
      """ |> tpkl.sh
    }
  }

  // Task arguments to embedded shell command from listing property
  ["sh-argv"] {
    cmds {
      (shArgs) {
        cmd {
          ...tpkl.argv
        }
      }
    }
  }

  // Task arguments to embedded shell command from listing property
  // with prepended argument
  ["sh-argv-partial"] {
    cmds {
      (shArgs) {
        cmd {
          "arg"
          ...tpkl.argv
        }
      }
    }
  }

  // Task arguments to command from listing property
  ["cmd-argv"] {
    cmds {
      new {
        embeddedShell = false
        cmd {
          "echo"
          ...tpkl.argv
        }
      }
    }
  }

  // Task arguments to command from listing property with // prepended
  // argument
  ["cmd-argv-partial"] {
    cmds {
      new {
        embeddedShell = false
        cmd {
          "echo"
          "arg"
          ...tpkl.argv
        }
      }
    }
  }
}
